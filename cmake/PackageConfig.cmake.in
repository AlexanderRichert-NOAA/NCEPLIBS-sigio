@PACKAGE_INIT@

#@PROJECT_NAME@-config.cmake
#
# Valid Find COMPONENTS:
#  * SHARED - Find shared libraries.
#  * STATIC - Find static libraries.
#
# Output variables set:
#  * @PROJECT_NAME@_FOUND
#  * @PROJECT_NAME@_STATIC_FOUND
#  * @PROJECT_NAME@_SHARED_FOUND
#
# Imported interface targets provided:
#  * @PROJECT_NAME@::@PROJECT_NAME@_4_static - real32 static library target
#  * @PROJECT_NAME@::@PROJECT_NAME@_8_static - real64 static library target
#  * @PROJECT_NAME@::@PROJECT_NAME@_4_shared - real32 shared library target
#  * @PROJECT_NAME@::@PROJECT_NAME@_8_shared - real64 shared library target

# Include targets file.  This will create IMPORTED target @PROJECT_NAME@
include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-targets.cmake")

set(@PROJECT_NAME@_SHARED_FOUND 0)
set(@PROJECT_NAME@_STATIC_FOUND 0)
if(@BUILD_STATIC_LIBS@)
    set(@PROJECT_NAME@_STATIC_FOUND 1)
    if(@BUILD_SHARED_LIBS@)
        set(@PROJECT_NAME@_SHARED_FOUND 1)
    endif()
elseif(@BUILD_SHARED_LIBS@)
    set(@PROJECT_NAME@_SHARED_FOUND 1)
else()
    set(@PROJECT_NAME@_FOUND 0)
    set(@PROJECT_NAME@_NOT_FOUND_MESSAGE "Neither BUILD_SHARED_LIBS nor BUILD_STATIC_LIBS was set.  Unable to find any libraries.")
endif()

# Get the build type from real32 library target
if(@BUILD_STATIC_LIBS@)
  get_target_property(@PROJECT_NAME@_BUILD_TYPES @PROJECT_NAME@::@PROJECT_NAME@_4_static IMPORTED_CONFIGURATIONS)
elseif(@BUILD_SHARED_LIBS@)
  get_target_property(@PROJECT_NAME@_BUILD_TYPES @PROJECT_NAME@::@PROJECT_NAME@_4_shared IMPORTED_CONFIGURATIONS)
endif()

check_required_components("@PROJECT_NAME@")
